import com.hypherionmc.modpublisher.properties.CurseEnvironment
import com.hypherionmc.modpublisher.properties.ReleaseType

plugins {
    id("architectury-plugin") version "3.5-SNAPSHOT"
    id("dev.architectury.loom-no-remap") version "1.17-SNAPSHOT" apply false
    id("com.gradleup.shadow") version "9.4.2" apply false
    id("com.hypherionmc.modutils.modpublisher") version "2.+"
    java
    `maven-publish`
}

val minecraftVersion = providers.gradleProperty("minecraft_version").get()
architectury.minecraft = minecraftVersion

allprojects {
    version = providers.gradleProperty("mod_version").get()
    group = providers.gradleProperty("maven_group").get()
}

subprojects {
    pluginManager.apply("dev.architectury.loom-no-remap")
    pluginManager.apply("architectury-plugin")
    pluginManager.apply("maven-publish")
    pluginManager.apply("com.hypherionmc.modutils.modpublisher")

    base.archivesName.set(providers.gradleProperty("archives_base_name").get() + "-${project.name}")

    repositories {
        mavenCentral()
        mavenLocal()
        maven("https://maven.fabricmc.net/")
        maven("https://maven.minecraftforge.net/")
        maven("https://maven.neoforged.net/releases/")
        maven("https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/").content {
            includeGroup("com.geckolib")
        }
        maven("https://jitpack.io")
        maven("https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1")
        maven("https://maven.jt-dev.tech/releases")
    }

    dependencies {
        "minecraft"("com.mojang:minecraft:$minecraftVersion")

        compileOnly("com.google.auto.service:auto-service:1.1.1")
        annotationProcessor("com.google.auto.service:auto-service:1.1.1")
    }

    java {
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_25
        targetCompatibility = JavaVersion.VERSION_25
    }

    tasks.withType<JavaCompile>().configureEach {
        options.release.set(25)
    }

    publishing {
        publications.create<MavenPublication>("mavenJava") {
            artifactId = base.archivesName.get()
            version = "${version}-mc$minecraftVersion"
            from(components["java"])
        }

        repositories {
            mavenLocal()
            maven {
                val releasesRepoUrl = "https://maven.jt-dev.tech/releases"
                val snapshotsRepoUrl = "https://maven.jt-dev.tech/snapshots"
                url = uri(if (project.version.toString().contains("SNAPSHOT") || project.version.toString().startsWith("0")) snapshotsRepoUrl else releasesRepoUrl)
                name = "JTDev-Maven-Repository"
                credentials {
                    username = providers.gradleProperty("repoLogin").orNull
                    password = providers.gradleProperty("repoPassword").orNull
                }
            }
        }
    }

    if (project.name != "Common")
        publisher {
            apiKeys {
                curseforge(getPublishingCredentials().first)
                modrinth(getPublishingCredentials().second)
                github(providers.gradleProperty("github_token").orNull)
            }
            displayName.set(base.archivesName.get() + "-${project.version}-mc$minecraftVersion")
            artifact.set(project.provider { project.tasks.named("shadowJar").get() })
            projectVersion.set(project.version.toString() + "-${project.name}")
            changelog.set(projectDir.toPath().parent.resolve("CHANGELOG.md").toFile().readLines().take(100).joinToString("\n"))
            curseID.set("1204282")
            modrinthID.set("909sOSOR")
            githubRepo.set("https://github.com/Potion-Studios/Wayfinder")
            setReleaseType(ReleaseType.RELEASE)
            setGameVersions(minecraftVersion, "26.1.1", "26.1")
            setCurseEnvironment(CurseEnvironment.BOTH)
            setJavaVersions(JavaVersion.VERSION_25)
        }
}

private fun getPublishingCredentials(): Pair<String?, String?> {
    val curseForgeToken = (project.findProperty("curseforge_token") ?: System.getenv("CURSEFORGE_TOKEN") ?: "") as String?
    val modrinthToken = (project.findProperty("modrinth_token") ?: System.getenv("MODRINTH_TOKEN") ?: "") as String?
    return Pair(curseForgeToken, modrinthToken)
}
